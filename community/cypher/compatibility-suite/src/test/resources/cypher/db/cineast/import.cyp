LOAD CSV WITH HEADERS FROM {user_csv} AS line CREATE (:User {name: line.name, login: line.login, password: line.password});
USING PERIODIC COMMIT 500 LOAD CSV WITH HEADERS FROM {person_csv} AS line CREATE (p:Person {id: line.id}) SET p = line SET p.lastModified = toInt(line.lastModified) SET p.version = toInt(line.version);
USING PERIODIC COMMIT 500 LOAD CSV WITH HEADERS FROM {movie_csv} AS line CREATE (m:Movie {id: line.id}) SET m = line SET m.version = toInt(line.version) SET m.lastModified = toInt(line.lastModified) SET m.releaseDate = toInt(line.releaseDate) SET m.runtime = toInt(line.runtime) SET m.tagline = coalesce(line.tagline, "") SET m.homepage = coalesce(line.homepage, "") SET m.language = coalesce(line.language, "") SET m.description = coalesce(line.description, "");
CREATE CONSTRAINT ON (u:User) ASSERT u.login IS UNIQUE;
CREATE CONSTRAINT ON (n:Person) ASSERT n.id IS UNIQUE;
CREATE CONSTRAINT ON (m:Movie) ASSERT m.id IS UNIQUE;
USING PERIODIC COMMIT 500 LOAD CSV WITH HEADERS FROM {acts_in_csv} AS line MERGE (a:Person {id: line.actorId}) MERGE (m:Movie {id: line.movieId}) SET a:Actor CREATE (a)-[:ACTS_IN {name: line.roleName}]->(m);
USING PERIODIC COMMIT 500 LOAD CSV WITH HEADERS FROM {directed_csv} AS line MERGE (d:Person {id: line.directorId}) MERGE (m:Movie {id: line.movieId}) SET d:Director CREATE (d)-[:DIRECTED]->(m);
LOAD CSV WITH HEADERS FROM {friend_csv} AS line MERGE (u1:User {login: line.user1}) MERGE (u2:User {login: line.user2}) CREATE (u1)-[:FRIEND]->(u2);
LOAD CSV WITH HEADERS FROM {rated_csv} AS line MERGE (u:User {login: line.user}) MERGE (m: Movie {id: line.movieId}) MERGE (u)-[:RATED {stars: line.stars, comment: line.comment}]->(m);
CREATE INDEX ON :Actor(name);
CREATE INDEX ON :Director(name);
CREATE INDEX ON :Movie(title);
CREATE INDEX ON :Person(name);
